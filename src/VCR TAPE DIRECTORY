10  REM ************************
20  REM *  VCR TAPE DIRECTORY  *
30  REM *         BY           *
40  REM *   TRANSNEPTUNIAN     *
50  REM *                      *
60  REM * (C) 1987 - 2020      *
70  REM ************************
80  REM 
90  HOME 
100  DIM M$(18)
110  UP$ =  CHR$ (1):LO$ =  CHR$ (26):D$ =  CHR$ (4):M = 1
130  A =  PEEK (3264): FOR B = 1 TO A:L$ = L$ +  CHR$ ( PEEK (3264 + B)): NEXT B
140  IF  PEEK (2052) *  PEEK (2053) = 12844 THEN 180
150  PRINT  CHR$ (4)"BRUN  WINDOW"
160  PRINT D$"OPEN "L$",L150": PRINT D$"READ "L$",R0": INPUT NT: PRINT D$"CLOSE"
170  POKE 3312,NT
180  REM ** SETUP **
190  FOR A = 1 TO 17: READ M$(A): NEXT A
200  NT =  PEEK (3312): REM NUMBER OF TAPES
202  FOR A = 1 TO 16: READ A$: IF A$ = "*" THEN A$ =  CHR$ (1)
203  IF A$ = "-" THEN A$ =  CHR$ (26)
204  H$(1) = H$(1) + A$: NEXT A
206  FOR A = 1 TO 8: READ A$: IF A$ = "*" THEN A$ =  CHR$ (1)
208  IF A$ = "-" THEN A$ =  CHR$ (26)
210  H$(2) = H$(2) + A$: NEXT A
212  FOR A = 1 TO 7: READ A$: IF A$ = "*" THEN A$ =  CHR$ (1)
214  IF A$ = "-" THEN A$ =  CHR$ (26)
216  H$(3) = H$(3) + A$: NEXT A
219  GOTO 300
220  DATA FOR CERTAIN CODE,FOR TITLE,FOR TAPE #
230  DATA GO BACK
240  DATA NEW TAPE,ADD PROGRAM,CLEAR TAPE,REMOVE MOVIE
250  DATA GO BACK
260  DATA TAPE LIBRARY,ONE TAPE,BY CODE LETTERS
270  DATA GO BACK
280  DATA MOVIE NAME,MOVIE ORDER,MOVIE CODES
290  DATA GO BACK
292  DATA VCR T,-,APE ,*,D,-,IRECTORY ,*,P,-,ROGRAM ,*,V,-,ER 1.0,*
294  DATA  B,-,Y ,*,T,-,RANSNEPTUNIAN,*
296  DATA -,(C) 1987 ,*,P-ETM E,-,ENTERPRISES,*
300  & NEW "  EXAMINE  SEARCH  UPDATE  EDIT  PRINT",1,38,7,15
310  VTAB 24: INVERSE : PRINT "USE <- -> AND RETURN TO SELECT OPTIONS";: NORMAL 
320  VTAB 10: HTAB 3: PRINT H$(1)
330  PRINT : HTAB 12: PRINT H$(2): PRINT : HTAB 6: PRINT H$(3)
340  INVERSE : GOSUB 450: NORMAL 
350  WAIT  - 16384,128:K =  PEEK ( - 16384): IF K <  > 149 AND K <  > 136 AND K <  > 141 THEN  POKE  - 16368,0: GOTO 350
360  POKE  - 16368,0: IF K = 141 THEN 440
370  GOSUB 450
380  ON K <  > 136 GOTO 410:M = M - 1: IF M = 0 THEN M = 5
390  INVERSE : GOSUB 450
400  POKE  - 16368,0: NORMAL : GOTO 350
410  M = M + 1: IF M = 6 THEN M = 1
420  INVERSE : GOSUB 450
430  POKE  - 16368,0: NORMAL : GOTO 350
440  ON M GOTO 510,810,870,930,990
450  VTAB 1: IF M = 1 THEN  HTAB 2: PRINT "EXAMINE"
460  IF M = 2 THEN  HTAB 11: PRINT "SEARCH"
470  IF M = 3 THEN  HTAB 19: PRINT "UPDATE"
480  IF M = 4 THEN  HTAB 27: PRINT "EDIT"
490  IF M = 5 THEN  HTAB 33: PRINT "PRINT"
500  RETURN 
510  REM EXAMINE LIBRARY FILE
520  IF NT = 0 THEN 780
530  R = 1
540  & DRAW 1,38,17,22
550  GOSUB 1050: REM SHOW CODES
560  & DRAW 4,32,16,17: PRINT "  PRESS 'J' TO JUMP TO A TAPE";
570  & DRAW 1,38,3,15
580  VTAB 4: HTAB 1: PRINT : PRINT D$"OPEN "L$",L150"
590  PRINT : GOTO 680
600  POKE  - 16368,0: WAIT  - 16384,128:K =  PEEK ( - 16384): IF K <  > 149 AND K <  > 136 AND K <  > 202 AND K <  > 141 AND K <  > 155 THEN  GOTO 600
610  POKE  - 16368,0
620  IF K = 141 THEN  PRINT D$"CLOSE": RUN 
630  IF K = 202 THEN  GOSUB 720: GOTO 680
640  IF K = 149 THEN 670
650  R = R - 1: IF R = 0 THEN R = NT
660  GOTO 680
670  R = R + 1: IF R > NT THEN R = 1
680  GOSUB 1120: REM READ TAPE
690  VTAB 5: HTAB 5: PRINT "  TAPE #"TN SPC( 3) TAB( 33)"CODE"
700  FOR A = 1 TO 6: VTAB 6 + A: HTAB 4: PRINT A;") ";: PRINT MV$(A); SPC( 21 -  LEN (MV$(A)));
710  PRINT  TAB( 33)MC$(A)" ": NEXT A: PRINT : GOTO 600
720  PRINT D$"CLOSE": POKE  - 16368,0
730  & STORE 9,20,9,13
740  PRINT : HTAB 2: INPUT "JUMP TO TAPE #";JT$:JT =  VAL (JT$)
750  IF JT =  > 1 AND JT <  = NT THEN R = JT
760  PRINT D$"OPEN "L$",L150"
770  & RESTORE : RETURN 
780  REM NO TAPES YET
790  & STORE 8,22,5,9: PRINT : PRINT " YOU MUST ADD A TAPE": PRINT "    TO THE LIBRARY"
800  GOSUB 1150: & RESTORE : & RESTORE : GOTO 350
810  REM  SEARCH LIBRARY FILE
820  M1 = 1:M2 = 4:D = 4
830  & STORE 11,17,2,7
840  GOSUB 1160: REM   MENU
850  ON X GOTO 3710,3420,3280
860  & RESTORE : GOTO 350
870  REM  UPDATE LIBRARY FILE
880  M1 = 5:M2 = 9:D = 5
890  & STORE 19,13,2,8
900  GOSUB 1160: REM  MENU
910  ON X GOTO 1310,1440,1730,1930
920  POKE  - 16368,0: & RESTORE : GOTO 350
930  REM  EDIT LIBRARY FILE
940  M1 = 14:M2 = 17:D = 4
950  & STORE 25,12,2,7
960  GOSUB 1160: REM MENU
970  ON X GOTO 2190,2630,2990
980  POKE  - 16368,0: & RESTORE : GOTO 350
990  REM  PRINT LIBIRARY FILE
1000  M1 = 10:M2 = 13:D = 4
1010  & STORE 23,16,2,7
1020  GOSUB 1160: REM   MENU 
1030  ON X GOTO 3940,4350,4490
1040  POKE  - 16368,0: & RESTORE : GOTO 350
1050  REM SHOW CODES
1060  PRINT D$"OPEN "L$",L150": PRINT D$"READ "L$",R0": INPUT NT: FOR A = 1 TO 8: INPUT CO$(A): INPUT DS$(A): NEXT A
1070  FOR A = 1 TO 7 STEP 2: VTAB (18 + (A / 2 + .5)): HTAB 2: PRINT CO$(A)"-" TAB( 7)DS$(A);
1080  PRINT  TAB( 21)CO$(A + 1)"-" TAB( 26)DS$(A + 1);: NEXT A: VTAB 4: PRINT 
1090  PRINT D$"CLOSE": RETURN 
1100  REM READ TAPE
1110  PRINT D$"OPEN "L$",L150"
1120  PRINT D$"READ "L$",R";R
1130  INPUT TN: FOR A = 1 TO 6: INPUT MV$(A): INPUT MC$(A): NEXT A
1140  RETURN 
1150  POKE  - 16368,0: WAIT  - 16384,128: POKE  - 16368,0: RETURN 
1160  REM  MENU
1170  FOR A = M1 TO M2: PRINT M$(A): NEXT 
1180  INVERSE : VTAB 3: PRINT M$(M1): NORMAL 
1190  POKE  - 16368,0:X = 1
1200  WAIT  - 16384,128:K =  PEEK ( - 16384): IF K <  > 149 AND K <  > 136 AND K <  > 141 THEN  POKE  - 16368,0: GOTO 1200
1210  IF K = 141 THEN 1300
1220  GOSUB 1290
1230  ON K <  > 136 GOTO 1260:X = X - 1: IF X = 0 THEN X = D
1240  INVERSE : GOSUB 1290
1250  POKE  - 16368,0: NORMAL : GOTO 1200
1260  X = X + 1: IF X = D + 1 THEN X = 1
1270  INVERSE : GOSUB 1290
1280  POKE  - 16368,0: NORMAL : GOTO 1200
1290  VTAB 2 + X: PRINT M$(M1 + X - 1)
1300  POKE  - 16368,0: RETURN 
1310  REM ADD TAPE
1320  & STORE 15,23,6,10
1330  & HOME : PRINT : PRINT " ADD TAPE #"(NT + 1)" (Y/N)?";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 1330
1340  IF A$ = "N" THEN  & RESTORE : & RESTORE : GOTO 350
1350  PRINT 
1360  PRINT D$"OPEN "L$",L150": PRINT D$"READ "L$",R0": INPUT T: FOR A = 1 TO 8: INPUT CO$(A): INPUT DS$(A): NEXT A: PRINT D$"CLOSE"
1370  PRINT D$"OPEN "L$",L150": PRINT D$"WRITE "L$",R0":NT = NT + 1: PRINT NT
1380  FOR A = 1 TO 8: PRINT CO$(A): PRINT DS$(A): NEXT A
1390  PRINT D$"WRITE ";L$;",R";NT
1400  PRINT NT: FOR A = 1 TO 12: PRINT "": NEXT A: PRINT D$"CLOSE"
1410  POKE 3312,NT
1420  & HOME : PRINT : PRINT " TAPE #"NT" IS ADDED": PRINT "  PRESS A KEY  ": GOSUB 1150
1430  & RESTORE : & RESTORE : GOTO 350
1440  REM ADD PROGRAM
1450  IF NT = 0 THEN 780
1460  & STORE 15,20,6,10
1470  & HOME : PRINT : PRINT "ADD PROGRAM TO TAPE     (1-"NT") ";: INPUT "";A$
1480  & RESTORE : & RESTORE : IF  VAL (A$) < 1 OR  VAL (A$) > NT THEN 350
1490  R =  VAL (A$)
1500  & DRAW 1,38,18,22
1510  GOSUB 1050
1520  & DRAW 1,38,3,15
1530  GOSUB 1100: PRINT D$"CLOSE"
1540  SL = 7: FOR A = 6 TO 1 STEP  - 1: IF MV$(A) = "" THEN SL = A
1550  NEXT A
1560  VTAB 5: HTAB 8: PRINT "TAPE #"TN TAB( 32)"CODE"
1570  VTAB 7: FOR A = 1 TO 6: HTAB 4: PRINT A") "MV$(A) SPC( 21 -  LEN (MV$(A)));: HTAB 33: PRINT MC$(A): NEXT A
1580  IF SL = 7 THEN  & DRAW 12,17,14,18: PRINT : PRINT "  ALL SLOTS FULL": PRINT "  PRESS A KEY": POKE  - 16368,0: WAIT  - 16384,128: POKE  - 16368,0: RUN 
1590  VTAB 6 + SL: HTAB 7: INPUT "";PN$
1600  IF PN$ = "" THEN  GOSUB 2970: GOTO 1590
1610  VTAB 6 + SL: HTAB 7: PRINT  LEFT$ (PN$,20) SPC( 12)
1620  VTAB 6 + SL: HTAB 33: INPUT "";CD$: IF CD$ = "" THEN 1620
1630  CD$ = CD$ + "   ": & STORE 9,23,(7 + SL),(10 + SL)
1640  & HOME : PRINT : PRINT " CORRECT (Y/N) ";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 1640
1650  IF A$ = "N" THEN  & RESTORE : GOTO 1540
1660  MV$(SL) =  LEFT$ (PN$,20):MC$(SL) =  LEFT$ (CD$,3)
1670  & HOME : PRINT : PRINT " UPDATING FILE ..."
1680  PRINT D$"OPEN "L$",L150": PRINT D$"WRITE "L$",R"R
1690  PRINT TN: FOR A = 1 TO 6: PRINT MV$(A): PRINT MC$(A): NEXT A: PRINT D$"CLOSE"
1700  POKE  - 16368,0: & HOME : PRINT : PRINT "  ADD ANOTHER (Y/N) ";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 1700
1710  IF A$ = "N" THEN  RUN 
1720  & RESTORE : GOTO 1540
1730  REM DELETE TAPE
1740  IF NT = 0 THEN 780
1750  & STORE 15,20,6,10
1760  & HOME : PRINT : PRINT "DELETE WHICH TAPE     (1-"NT") ";: INPUT "";A$
1770  & RESTORE : & RESTORE : IF  VAL (A$) < 1 OR  VAL (A$) > NT THEN 350
1780  & DRAW 1,38,18,22: GOSUB 1050
1790  R =  VAL (A$): GOSUB 1100
1800  PRINT D$"CLOSE"
1810  & DRAW 1,38,3,15
1820  VTAB 5: HTAB 5: PRINT "  TAPE #"TN"   " TAB( 33)"CODE"
1830  FOR A = 1 TO 6: VTAB 6 + A: HTAB 4: PRINT A;") ";: PRINT MV$(A); SPC( 21 -  LEN (MV$(A)));
1840  PRINT  TAB( 33)MC$(A)" ": NEXT A: PRINT 
1850  & STORE 8,25,14,17
1860  & HOME : PRINT : PRINT " DELETE THIS TAPE (Y/N)";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 1860
1870  IF A$ = "N" THEN  RUN 
1880  & HOME : PRINT : PRINT " REMOVING ALL PROGRAMS"
1890  PRINT D$"OPEN "L$",L150": PRINT D$"WRITE "L$",R"R
1900  PRINT TN: FOR A = 1 TO 12: PRINT "": NEXT A
1910  PRINT D$"CLOSE"
1920  & HOME : PRINT : PRINT " PRESS ANY KEY": GOSUB 1150: RUN 
1930  REM REMOVE MOVIE
1940  IF NT = 0 THEN 780
1950  & STORE 15,20,6,10
1960  & HOME : PRINT : PRINT " REMOVE MOVIE FROM   TAPE (1-"NT") ";: INPUT "";A$
1970  & RESTORE : & RESTORE : IF  VAL (A$) < 1 OR  VAL (A$) > NT THEN 350
1980  R =  VAL (A$)
1990  GOSUB 1100: PRINT D$"CLOSE"
2000  & DRAW 1,38,3,15
2010  VTAB 5: HTAB 5: PRINT "  TAPE #"TN"   " TAB( 33)"CODE"
2020  SL = 6
2030  FOR A = 1 TO 6: VTAB 6 + A: HTAB 4: PRINT A;") ";: PRINT MV$(A); SPC( 21 -  LEN (MV$(A)));: HTAB 33: PRINT MC$(A)
2040  IF MV$(A) = "" THEN SL = SL - 1
2050  NEXT A: IF SL = 0 THEN 2170
2060  & STORE 8,25,14,17
2070  & HOME : PRINT : PRINT " REMOVE PROGRAM (1-"SL") ";: GET A$: IF  VAL (A$) < 1 OR  VAL (A$) > SL THEN  GOSUB 2970: GOTO 2070
2080  PR =  VAL (A$): & RESTORE : VTAB 6 + PR: HTAB 7: INVERSE : PRINT MV$(PR) SPC( 26 -  LEN (MV$(PR)))MC$(PR): NORMAL : & STORE 8,25,14,17
2090  & STORE 10,20,14,17
2100  & HOME : PRINT : PRINT " ARE YOU SURE (Y/N)";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 2100
2110  IF A$ = "N" THEN  RUN 
2120  & RESTORE : & HOME : PRINT : PRINT " REMOVING #"PR" ..."
2130  FOR A = PR TO 6:MV$(A) = MV$(A + 1):MC$(A) = MC$(A + 1): NEXT A:MV$(6) = "":MC$(6) = ""
2140  PRINT D$"OPEN "L$",L150": PRINT D$"WRITE "L$",R"R
2150  PRINT TN: FOR A = 1 TO 6: PRINT MV$(A): PRINT MC$(A): NEXT A: PRINT D$"CLOSE"
2160  & HOME : PRINT : PRINT " PRESS ANY KEY": GOSUB 1150: RUN 
2170  & DRAW 8,25,13,16
2180  PRINT : PRINT " ALL SLOTS ARE EMPTY": PRINT " PRESS ANY KEY": GOSUB 1150: RUN 
2190  REM CHANGE PROGRAM NAME
2200  IF NT = 0 THEN 780
2210  & STORE 18,20,6,10
2220  & HOME : PRINT : PRINT " CHANGE NAME FROM    TAPE (1-"NT") ";: INPUT "";A$
2230  & RESTORE : & RESTORE : IF  VAL (A$) < 1 OR  VAL (A$) > NT THEN 350
2240  R =  VAL (A$)
2250  GOSUB 1100: PRINT D$"CLOSE"
2260  & DRAW 1,38,17,22: GOSUB 1050
2270  & DRAW 1,38,3,15
2280  VTAB 5: HTAB 5: PRINT "  TAPE #"TN"   " TAB( 33)"CODE"
2290  FOR A = 1 TO 6: VTAB 6 + A: HTAB 4: PRINT A;") ";: PRINT MV$(A); SPC( 21 -  LEN (MV$(A)));: HTAB 33: PRINT MC$(A): NEXT A
2300  SL = 6: FOR A = 6 TO 1 STEP  - 1: IF MV$(A) = "" THEN SL = SL - 1
2310  NEXT A
2320  IF SL = 0 THEN  & DRAW 8,25,15,19: PRINT : PRINT " ALL SLOTS ARE EMPTY": PRINT " PRESS ANY KEY": GOSUB 1150: RUN 
2330  & STORE 7,29,14,17:XX = SL
2340  & HOME : PRINT : PRINT " CHANGE WHICH NAME (1-"SL") ";: GET A$: IF  VAL (A$) < 1 OR  VAL (A$) > SL THEN  GOSUB 2970: GOTO 2340
2350  SL =  VAL (A$): INVERSE : VTAB 6 + SL: HTAB 1: PRINT MV$(SL) SPC( 26 -  LEN (MV$(SL)));: HTAB 27: PRINT MC$(SL): NORMAL 
2360  & HOME : PRINT : PRINT "  ENTER NEW NAME AND CODE": & STORE 6,32,(7 + SL),(9 + SL)
2370  VTAB 8 + SL: HTAB 2: INPUT "";PN$: IF PN$ <  > "" THEN 2410
2380  & RESTORE 
2390  & HOME : PRINT : PRINT " DO YOU WANT TO EXIT (Y/N)";: GET A$: IF A$ = "Y" THEN  RUN 
2400  GOTO 2360
2410  VTAB 8 + SL: HTAB 2: PRINT  LEFT$ (PN$,20) SPC( 8)
2420  VTAB 8 + SL: HTAB 28: INPUT "";CD$: IF CD$ = "" THEN 2420
2430  CD$ = CD$ + "   "
2440  VTAB 8 + SL: HTAB 28: PRINT  LEFT$ (CD$,3) SPC( 3)
2450  & RESTORE : & RESTORE 
2460  INVERSE : VTAB 6 + SL: HTAB 7: PRINT  LEFT$ (PN$,20) SPC( 8);: HTAB 33: PRINT  LEFT$ (CD$,3): NORMAL 
2470  & STORE 7,26,13,16
2480  & HOME : PRINT : PRINT " SAVE THIS CHANGE (Y/N) ";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 2480
2490  IF A$ = "Y" THEN 2550
2500  & RESTORE : VTAB 6 + SL: HTAB 7: PRINT MV$(SL) SPC( 26 -  LEN (MV$(SL)));: HTAB 33: PRINT MC$(SL)
2510  POKE  - 16368,0: & STORE 7,28,13,16
2520  & HOME : PRINT : PRINT " CHANGE ANOTHER ONE (Y/N) ";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 2520
2530  IF A$ = "N" THEN  RUN 
2540  & RESTORE :SL = XX: GOTO 2330
2550  & HOME : PRINT : PRINT " SAVING THE CHANGE..."
2560  PRINT D$"OPEN "L$",L150": PRINT D$"WRITE "L$",R"R
2570  MV$(SL) =  LEFT$ (PN$,20):MC$(SL) =  LEFT$ (CD$,3)
2580  PRINT TN: FOR A = 1 TO 6: PRINT MV$(A): PRINT MC$(A): NEXT A: PRINT D$"CLOSE"
2590  & HOME : PRINT : PRINT " CHANGE ANOTHER ONE (Y/N) ";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 2590
2600  IF A$ = "N" THEN  RUN 
2610  & RESTORE : & RESTORE : GOTO 2270
2620  END 
2630  REM MOVIE ORDER
2640  IF NT = 0 THEN 780
2650  & STORE 18,20,6,10
2660  & HOME : PRINT : PRINT " FROM WHICH TAPE     (1-"NT") ";: INPUT "";A$
2670  & RESTORE : & RESTORE : IF  VAL (A$) < 1 OR  VAL (A$) > NT THEN 350
2680  R =  VAL (A$)
2690  & DRAW 1,38,17,22: GOSUB 1050
2700  & DRAW 1,38,3,15: GOSUB 1100: PRINT D$"CLOSE"
2710  VTAB 5: HTAB 5: PRINT "  TAPE #"TN"   " TAB( 33)"CODE"
2720  SL = 6
2730  FOR A = 1 TO 6: VTAB 6 + A: HTAB 4: PRINT A;") ";: PRINT MV$(A); SPC( 21 -  LEN (MV$(A)));: HTAB 33: PRINT MC$(A)
2740  IF MV$(A) = "" THEN SL = SL - 1
2750  NEXT A: IF SL = 0 THEN 2170
2760  & STORE 8,25,13,16
2770  & HOME : PRINT : PRINT "EXCHANGE PROGRAM (1-"SL") ";: GET A$: IF  VAL (A$) < 1 OR  VAL (A$) > SL THEN  GOSUB 2970: GOTO 2770
2780  T =  VAL (A$):TM$ = MV$(T):TC$ = MC$(T)
2790  & RESTORE : INVERSE : VTAB 6 + T: HTAB 7: PRINT TM$ SPC( 26 -  LEN (TM$));: HTAB 33: PRINT TC$: NORMAL 
2800  & STORE 8,25,13,16
2810  & HOME : PRINT : PRINT "EXCHANGE "T" WITH (1-"SL") ";: GET A$: IF  VAL (A$) < 1 OR  VAL (A$) > SL OR  VAL (A$) = T THEN  GOSUB 2970: GOTO 2810
2820  S =  VAL (A$):SM$ = MV$(S):SC$ = MC$(S)
2830  & RESTORE : INVERSE : VTAB 6 + S: HTAB 7: PRINT SM$ SPC( 26 -  LEN (SM$));: HTAB 33: PRINT SC$: NORMAL 
2840  & STORE 8,25,13,16
2850  & HOME : PRINT : PRINT "SWITCH THESE TWO (Y/N) ";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 2850
2860  IF A$ = "Y" THEN 2890
2870  & RESTORE : VTAB 6 + T: HTAB 7: PRINT TM$ SPC( 26 -  LEN (TM$));: HTAB 33: PRINT TC$: VTAB 6 + S: HTAB 7: PRINT SM$ SPC( 26 -  LEN (SM$));: HTAB 33: PRINT SC$
2880  & STORE 8,25,13,16: GOTO 2940
2890  MV$(T) = SM$:MC$(T) = SC$:MV$(S) = TM$:MC$(S) = TC$
2900  & RESTORE : VTAB 6 + S: HTAB 7: PRINT TM$ SPC( 26 -  LEN (TM$));: HTAB 33: PRINT TC$: VTAB 6 + T: HTAB 7: PRINT SM$ SPC( 26 -  LEN (SM$));: HTAB 33: PRINT SC$
2910  & STORE 8,25,13,16: PRINT : PRINT " UPDATING FILE ..."
2920  PRINT D$"OPEN "L$",L150": PRINT D$"WRITE "L$",R"R
2930  PRINT TN: FOR A = 1 TO 6: PRINT MV$(A): PRINT MC$(A): NEXT A: PRINT D$"CLOSE"
2940  & HOME : PRINT : PRINT " ANOTHER SWITCH (Y/N)";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 2940
2950  IF A$ = "N" THEN  RUN 
2960  & RESTORE : GOTO 2760
2970  & STORE 8,25,16,19: PRINT : PRINT "LEAVE THIS OPTION (Y/N)";: GET A$: IF A$ = "Y" THEN  POP : RUN 
2980  & RESTORE : POKE  - 16368,0: RETURN 
2990  REM MAKE TAPE CODES
3000  & RESTORE 
3010  & DRAW 1,38,17,22: GOSUB 1050
3020  & DRAW 1,38,3,15
3030  VTAB 5: HTAB 7: PRINT "CODE" TAB( 14)"DESCRIPTION"
3040  FOR A = 1 TO 8: VTAB 6 + A: HTAB 4: PRINT A") "CO$(A);: HTAB 14: PRINT DS$(A)
3050  NEXT A
3060  & STORE 8,25,14,17
3070  & HOME : PRINT : PRINT "CHANGE WHICH ONE (1-8) ";: GET A$: IF  VAL (A$) < 1 OR  VAL (A$) > 8 THEN  GOSUB 2970: GOTO 3070
3080  CH =  VAL (A$): & RESTORE 
3090  VTAB 6 + CH: HTAB 7: INPUT "";C$: IF C$ = "" THEN  GOSUB 2970: GOTO 3090
3100  C$ = C$ + "   ":C$ =  LEFT$ (C$,3): VTAB 6 + CH: HTAB 7: PRINT C$ SPC( 32)
3110  VTAB 6 + CH: HTAB 14: INPUT "";S$: IF S$ = "" THEN  GOSUB 2970: GOTO 3110
3120  VTAB 6 + CH: HTAB 14: PRINT  LEFT$ (S$,12) SPC( 21 -  LEN ( LEFT$ (S$,12)))
3130  & STORE 8,25,(7 + CH),(10 + CH)
3140  & HOME : PRINT : PRINT " SAVE THIS CHANGE (Y/N)";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 3140
3150  IF A$ = "N" THEN 3210
3160  & HOME : PRINT : PRINT " SAVING CODE CHANGE"
3170  PRINT D$"OPEN "L$",L150": PRINT D$"WRITE "L$",R0"
3180  CO$(CH) = C$:DS$(CH) =  LEFT$ (S$,12)
3190  PRINT NT: FOR A = 1 TO 8: PRINT CO$(A): PRINT DS$(A): NEXT A: PRINT D$"CLOSE"
3200  GOTO 3230
3210  & RESTORE : VTAB 6 + CH: HTAB 6: PRINT  SPC( 30)
3220  & STORE 8,25,(7 + CH),(10 + CH)
3230  & HOME : PRINT : PRINT " ANOTHER CHANGE (Y/N)";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 3230
3240  POKE  - 16368,0
3250  IF A$ = "N" THEN  RUN 
3260  & RESTORE 
3270  GOTO 3060
3280  REM GET A TAPE
3290  IF NT = 0 THEN 780
3300  & STORE 15,17,6,10
3310  & HOME : PRINT : PRINT "WHICH TAPE       (1-"NT") ";: INPUT "";A$
3320  & RESTORE : & RESTORE : IF  VAL (A$) < 1 OR  VAL (A$) > NT THEN 350
3330  R =  VAL (A$)
3340  & DRAW 1,38,18,22
3350  GOSUB 1050: & DRAW 1,38,3,15: GOSUB 1100: PRINT D$"CLOSE"
3360  VTAB 5: HTAB 7: PRINT "TAPE #"TN SPC( 3) TAB( 33)"CODE"
3370  FOR A = 1 TO 6: VTAB 6 + A: HTAB 4: PRINT A;") ";: PRINT MV$(A); SPC( 21 -  LEN (MV$(A)));: HTAB 33: PRINT MC$(A): NEXT A
3380  & DRAW 8,25,13,16
3390  & HOME : PRINT : PRINT "  PRESS <SPACE> TO EXIT": POKE  - 16368,0: WAIT  - 16384,128
3400  K =  PEEK ( - 16384): IF K <  > 160 THEN 3390
3410  POKE  - 16368,0: RUN 
3420  REM FIND A PROGRAM
3430  IF NT = 0 THEN 780
3440  N = 1
3450  & STORE 6,25,5,9
3460  & HOME : INVERSE : PRINT  SPC( 7)"SEARCH FOR" SPC( 8);: NORMAL : PRINT : HTAB 2: PRINT " ";: INPUT "";S$: IF S$ = "" THEN  & RESTORE : & RESTORE : GOTO 350
3470  S$ =  LEFT$ (S$,20)
3480  & HOME : INVERSE : PRINT  SPC( 7)"SEARCH FOR" SPC( 8);: NORMAL : PRINT : PRINT  SPC(  INT (24 -  LEN (S$)) / 2)S$
3490  PRINT  SPC( 9)"(Y/N)";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 3480
3500  & RESTORE : & RESTORE : IF A$ = "N" THEN 350
3510  L =  LEN (S$)
3520  PRINT : PRINT D$"OPEN"L$",L150"
3530  & DRAW 1,38,3,20
3540  PRINT : PRINT " NAME" TAB( 25)"CODE" TAB( 32)"TAPE #": FOR Z = 1 TO 37: PRINT "-";: NEXT Z: PRINT 
3550  & STORE 6,27,7 + N,10 + N: FOR R = 1 TO NT: GOSUB 1120: & HOME : PRINT : PRINT " SEARCHING RECORD "R
3560  FOR CK = 1 TO 6: IF MV$(CK) = "" THEN 3600
3570  FOR S = 1 TO (21 - L)
3580  IF  MID$ (MV$(CK),S,L) = S$ THEN 3620
3590  NEXT S
3600  NEXT CK
3610  NEXT R: GOTO 3650
3620  & RESTORE 
3630  N = N + 1: IF N = 14 THEN 3660
3640  VTAB 6 + N: PRINT " "MV$(CK) TAB( 25)MC$(CK) TAB( 33)R: & STORE 6,27,7 + N,10 + N: PRINT : PRINT " SEARCHING RECORD "R: GOTO 3600
3650  & STORE 6,27,7 + N,10 + N: PRINT D$"CLOSE": PRINT : PRINT " PRESS ANY KEY TO CONTINUE": GOSUB 1150: RUN 
3660  & STORE 6,27,20,23
3670  PRINT : PRINT " PRESS ANY KEY TO CONTINUE": GOSUB 1150
3680  & RESTORE : & HOME 
3690  PRINT : PRINT " NAME" TAB( 25)"CODE" TAB( 32)"TAPE #": FOR Z = 1 TO 37: PRINT "-";: NEXT Z: PRINT 
3700  PRINT :N = 2: GOTO 3640
3710  REM SEARCH FOR CODE
3720  IF NT = 0 THEN 780
3730  N = 1: & RESTORE 
3740  & STORE 1,38,17,22: GOSUB 1050
3750  & STORE 6,25,14,17
3760  & HOME : PRINT : PRINT " WHICH CODE ? ";: INPUT "";C$: & RESTORE : & RESTORE : IF C$ = "" THEN 350
3770  C$ = C$ + "   ":C$ =  LEFT$ (C$,3)
3780  PRINT D$"OPEN "L$",L150"
3790  & DRAW 1,38,3,20
3800  PRINT : PRINT " NAME" TAB( 25)"CODE" TAB( 32)"TAPE #": FOR Z = 1 TO 37: PRINT "-";: NEXT Z
3810  PRINT 
3820  & STORE 6,27,7 + N,10 + N: FOR R = 1 TO NT: GOSUB 1120: & HOME : PRINT : PRINT " SEARCHING RECORD "R
3830  FOR CK = 1 TO 6: IF MC$(CK) = "" THEN 3850
3840  IF MC$(CK) = C$ THEN 3860
3850  NEXT CK: NEXT R: GOTO 3920
3860  & RESTORE :N = N + 1: IF N = 14 THEN 3880
3870  VTAB 6 + N: PRINT " "MV$(CK) TAB( 25)MC$(CK) TAB( 33)R: & STORE 6,27,7 + N,10 + N: PRINT : PRINT " SEARCHING RECORD "R: GOTO 3850
3880  & STORE 6,27,20,23
3890  PRINT : PRINT " PRESS ANY KEY TO CONTINUE": POKE  - 16368,0: WAIT  - 16384,128: POKE  - 16368,0
3900  & RESTORE : & HOME : PRINT : PRINT " NAME" TAB( 25)"CODE" TAB( 32)"TAPE #": FOR Z = 1 TO 37: PRINT "-";: NEXT Z
3910  PRINT :N = 2: GOTO 3870
3920  & STORE 6,27,7 + N,10 + N: PRINT D$"CLOSE": PRINT : PRINT " PRESS ANY KEY TO CONTINUE": POKE  - 16368,0: WAIT  - 16384,128: POKE  - 16368,0: RUN 
3930  PRINT : PRINT " PRESS ANY KEY TO CONTINUE": GOSUB 1150
3940  REM PRINT LIBRARY
3950  IF NT = 0 THEN 780
3960  & STORE 15,20,6,10
3970  & HOME : PRINT : PRINT "PRINT WHOLE LIBRARY (Y/N)";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 3970
3980  & RESTORE : & RESTORE 
3990  IF A$ = "N" THEN 4240
4000  ST = 1:EN = NT: REM  WHOLE LIBRARY
4010  & STORE 4,32,11,14
4020  PRINT : PRINT " SETUP PRINTER AND PRESS A KEY": GOSUB 1150: & RESTORE 
4030  PRINT D$"OPEN "L$",L150"
4040  PRINT D$"PR#1": PRINT "   #" SPC( 4)"PROGRAM" SPC( 16)"CODE" SPC( 7)"PROGRAM" SPC( 16)"CODE": FOR B = 1 TO 79: PRINT "-";: NEXT B
4050  PRINT 
4060  FOR R = ST TO EN
4070  GOSUB 1120
4080  Z = Z + 1
4090  PRINT  SPC( 4 -  LEN ( STR$ (R)))R;
4100  PRINT  SPC( 4)MV$(1) SPC( 23 -  LEN (MV$(1)))MC$(1); SPC( 8)
4110  PRINT MV$(2) SPC( 23 -  LEN (MV$(2)))MC$(2)
4120  FOR A = 3 TO 6 STEP 2
4130  PRINT  SPC( 8)MV$(A) SPC( 23 -  LEN (MV$(A)))MC$(A); SPC( 8)
4140  PRINT MV$(A + 1) SPC( 23 -  LEN (MV$(A + 1)))MC$(A + 1)
4150  NEXT A: PRINT 
4160  IF Z = 14 THEN  GOSUB 4200
4170  NEXT R
4180  PRINT D$"CLOSE"
4190  GOSUB 1150: RUN 
4200  PRINT : PRINT  CHR$ (12)
4210  PRINT "   #" SPC( 4)"PROGRAM" SPC( 16)"CODE" SPC( 7)"PROGRAM" SPC( 16)"CODE": FOR B = 1 TO 79: PRINT "-";: NEXT B
4220  PRINT 
4230  Z = 0: RETURN 
4240  REM PRINT PORTION OF LIBRARY
4250  IF NT = 0 THEN 780
4260  & STORE 15,22,6,10
4270  & HOME : PRINT : PRINT "PRINT PART OF LIBRARY (Y/N) ";: GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 4270
4280  IF A$ = "N" THEN  & RESTORE : GOTO 350
4290  & HOME : PRINT : PRINT "PRINT FROM TAPE": PRINT "(1-"NT") ";: INPUT "";A$
4300  IF  VAL (A$) < 1 OR  VAL (A$) > NT THEN  & RESTORE : GOTO 350
4310  & HOME : PRINT : PRINT "PRINT FROM TAPE " VAL (A$): PRINT "TO TAPE (" VAL (A$)"-"NT") ";: INPUT "";B$
4320  & RESTORE 
4330  IF  VAL (B$) <  VAL (A$) OR  VAL (B$) > NT THEN 350
4340  ST =  VAL (A$):EN =  VAL (B$): GOTO 4010
4350  REM PRINT ONE TAPE
4360  IF NT = 0 THEN 780
4370  & STORE 15,20,6,10
4380  & HOME : PRINT : PRINT "PRINT WHICH TAPE    (1-"NT") ";: INPUT "";A$
4390  & RESTORE : & RESTORE 
4400  IF  VAL (A$) < 1 OR  VAL (A$) > NT THEN 350
4410  R =  VAL (A$)
4420  & STORE 4,32,11,14
4430  PRINT : PRINT " SETUP PRINTER AND PRESS A KEY": GOSUB 1150: & RESTORE 
4440  GOSUB 1100: PRINT D$"CLOSE"
4450  PRINT D$"PR#1"
4460  PRINT : PRINT " TAPE #"R: PRINT 
4470  FOR A = 1 TO 6: PRINT  SPC( 10)A") "MV$(A) SPC( 23 -  LEN (MV$(A)))MC$(A)
4480  NEXT A: PRINT : PRINT D$"PR#0": RUN 
4490  REM PRINT BY CODES
4500  IF NT = 0 THEN 780
4510  K = 0:N = 0:PG = 1
4520  & RESTORE 
4530  & STORE 1,38,17,22: GOSUB 1050: & STORE 6,25,14,17
4540  POKE  - 16368,0
4550  & HOME : PRINT : PRINT " WHICH CODE ? ";: INPUT "";C$
4560  & RESTORE : & RESTORE : IF C$ = "" THEN 350
4570  & STORE 2,36,10,16: PRINT 
4580  PRINT "    ENTER TITLE FOR TOP OF PAGE": PRINT "       40 CHARACTERS MAXIMUM": PRINT : INPUT "";T$
4590  T$ =  LEFT$ (T$,40)
4600  & RESTORE 
4610  & STORE 4,32,11,14
4620  PRINT : PRINT " SETUP PRINTER AND PRESS A KEY": GOSUB 1150: & RESTORE 
4630  PRINT D$"OPEN "L$",L150"
4640  PRINT D$"PR#1"
4650  PRINT  SPC( ( INT (41 -  LEN (T$)))) CHR$ (14)T$ CHR$ (18)
4660  PRINT : PRINT "PROGRAM" SPC( 16)"CODE" SPC( 3)"#" SPC( 7)"PROGRAM" SPC( 16)"CODE" SPC( 3)"#"
4670  FOR A = 1 TO 79: PRINT "-";: NEXT A: PRINT : FOR R = 1 TO NT
4680  GOSUB 1120
4690  FOR A = 1 TO 6
4700  IF MC$(A) = C$ THEN 4750
4710  NEXT A: NEXT R
4720  IF K = 1 THEN  PRINT F$(1) SPC( 23 -  LEN (F$(1)))FC$(1) SPC( 4)F(1)
4730  PRINT : PRINT : PRINT 
4740  PRINT D$"PR#0": PRINT D$"CLOSE": RUN 
4750  K = K + 1:F$(K) = MV$(A):FC$(K) = MC$(A):F(K) = R
4760  IF K <  > 2 THEN 4710
4770  K = 0
4780  N = N + 1: IF N = 50 THEN 4820
4790  PRINT F$(1) SPC( 23 -  LEN (F$(1)))FC$(1) SPC( 4)F(1) SPC( 8 -  LEN ( STR$ (F(1))));
4800  PRINT F$(2) SPC( 23 -  LEN (F$(2)))FC$(2) SPC( 4)F(2)
4810  GOTO 4710
4820  N = 0:PG = PG + 1: PRINT  CHR$ (12): PRINT  CHR$ (14)T$ CHR$ (18)
4830  PRINT  SPC( 38)"PAGE "PG: PRINT : PRINT "PROGRAM" SPC( 16)"CODE" SPC( 3)"#" SPC( 7)"PROGRAM" SPC( 16)"CODE" SPC( 3)"#"
4840  PRINT : FOR A = 1 TO 79: PRINT "-";: NEXT A: PRINT 
4850  GOTO 4790
